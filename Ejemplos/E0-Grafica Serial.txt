// Este código utiliza la biblioteca LVGL para mostrar una gráfica en tiempo real //

// Se lee la temperatura a través del puerto serie y se muestra en una gráfica de línea//



#include <Arduino.h>
#include <lvgl.h>
#include <TFT_eSPI.h>

TFT_eSPI tft = TFT_eSPI();

#define DRAW_BUF_SIZE (TFT_HEIGHT * TFT_WIDTH / 10 * (LV_COLOR_DEPTH / 8))
uint32_t draw_buf[DRAW_BUF_SIZE / 4];

static uint32_t my_tick(void)
{
    return millis();
}

lv_obj_t *graph;
lv_chart_series_t *temp_series;
lv_obj_t *temp_label;
lv_obj_t *label; 

#define GRAPH_HEIGHT 120
#define GRAPH_POINTS 10
int temperature_values[GRAPH_POINTS] = {0};

void setup()
{
    Serial.begin(115200); 
    lv_init();
    lv_tick_set_cb(my_tick);
    lv_display_t *disp = lv_tft_espi_create(TFT_WIDTH, TFT_HEIGHT, draw_buf, sizeof(draw_buf));

    lv_obj_set_style_bg_color(lv_scr_act(), lv_color_make(0, 0, 0), 0);

    label = lv_label_create(lv_scr_act());
    lv_label_set_text(label, "Temperatura en tiempo real");
    lv_obj_align(label, LV_ALIGN_TOP_MID, 0, 10);
    lv_obj_set_style_text_color(label, lv_color_make(255, 255, 255), 0);

    graph = lv_chart_create(lv_scr_act());
    lv_obj_set_size(graph, TFT_WIDTH, GRAPH_HEIGHT); 
    lv_obj_align(graph, LV_ALIGN_TOP_LEFT, 0, 40);   

    lv_chart_set_type(graph, LV_CHART_TYPE_LINE);               
    lv_chart_set_point_count(graph, GRAPH_POINTS);              
    lv_chart_set_range(graph, LV_CHART_AXIS_PRIMARY_Y, 0, 100); 

    temp_series = lv_chart_add_series(graph, lv_color_make(255, 50, 0), LV_CHART_AXIS_PRIMARY_Y);

    temp_label = lv_label_create(lv_scr_act());
    lv_label_set_text(temp_label, "TEMP: xx°C"); 
    lv_obj_set_pos(temp_label, 85, 170); 
    lv_obj_set_style_text_color(temp_label, lv_color_make(255, 255, 255), 0);
}

void loop()
{
    lv_timer_handler(); 

    if (Serial.available() > 0)
    {
        String received = Serial.readStringUntil('\n');
        received.trim();

        int temperature = received.toInt();

        if (temperature < 0)
            temperature = 0;
        if (temperature > 100)
            temperature = 100;

        for (int i = 0; i < GRAPH_POINTS - 1; i++)
        {
            temperature_values[i] = temperature_values[i + 1]; 
        }
        temperature_values[GRAPH_POINTS - 1] = temperature; 

        for (int i = 0; i < GRAPH_POINTS; i++)
        {
            lv_chart_set_next_value(graph, temp_series, temperature_values[i]); 
        }

        lv_label_set_text_fmt(temp_label, "TEMP: %d°C", temperature); 
    }

    delay(100); 
}
